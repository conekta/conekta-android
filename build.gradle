plugins {
    id 'com.android.library' version '7.3.1'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace "io.conekta"
    compileSdkVersion 33
    buildToolsVersion '34.0.0'
    useLibrary  'org.apache.http.legacy'
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 33
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

ext {
    swagger_annotations_version = "1.6.6"
    gson_version = "2.10.1"
    httpclient_version = "4.5.13"
    httpcore_version = "4.4.4"
    junit_version = "4.13"
}

dependencies {
    implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
    implementation "com.google.code.gson:gson:$gson_version"
    implementation "org.apache.httpcomponents:httpcore:$httpcore_version"
    implementation "org.apache.httpcomponents:httpclient:$httpclient_version"
    implementation ("org.apache.httpcomponents:httpcore:$httpcore_version") {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
    }
    implementation ("org.apache.httpcomponents:httpmime:$httpclient_version") {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
    }
    testImplementation "junit:junit:$junit_version"
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'io.conekta'
            artifactId = 'conektasdk'
            version = '6.0.0-beta'

            afterEvaluate {
                from components.release
            }
        }
        aar(MavenPublication) {
            groupId = 'io.conekta'
            artifactId = 'conektasdk'
            version = '6.0.0-beta'

            // Usa el archivo AAR generado
            artifact("$buildDir/outputs/aar/conektasdk-release.aar")

            pom {
                name = 'sdk conekta android'
                description = 'sdk conekta android'
                url = 'https://github.com/conekta/conekta-android'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }
                developers {
                    developer {
                        name = 'Conekta, Inc.'
                        email = 'core@conekta.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/conekta/conekta-android.git'
                    developerConnection = 'scm:git:ssh://github.com/conekta/conekta-android.git'
                    url = 'http://github.com/conekta/conekta-android'
                }
            }
        }
    }
   repositories {
        maven {
            name = 'OSSRH'
            url = uri('https://oss.sonatype.org/service/local/staging/deploy/maven2/')

            credentials {
                username = project.findProperty('ossrhUsername')
                password = project.findProperty('ossrhPassword')
            }
        }
    }
}

tasks.named('publishAarPublicationToOSSRHRepository').configure {
    dependsOn tasks.named('bundleReleaseAar')
}